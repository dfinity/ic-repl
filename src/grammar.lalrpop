use super::exp::{Field, Exp, Selector, Method, CallMode};
use candid::parser::types::{IDLType, TypeField, PrimType, FuncType, FuncMode, Binding};
use candid::parser::typing::{TypeEnv, check_unique};
use super::token::{Token, error2, LexicalError, Span};
use candid::{Principal, types::Label};
use super::command::{Command, Commands, BinOp};

grammar;

extern {
    type Location = usize;
    type Error = LexicalError;
    enum Token {
        "decimal" => Token::Decimal(<String>),
        "hex" => Token::Hex(<String>),
        "float" => Token::Float(<String>),
        "bool" => Token::Boolean(<bool>),
        "text" => Token::Text(<String>),
        "id" => Token::Id(<String>),
        "null" => Token::Null,
        "opt" => Token::Opt,
        "vec" => Token::Vec,
        "record" => Token::Record,
        "variant" => Token::Variant,
        "func" => Token::Func,
        "service" => Token::Service,
        "oneway" => Token::Oneway,
        "query" => Token::Query,
        "blob" => Token::Blob,
        "type" => Token::Type,
        "import" => Token::Import,
        "export" => Token::Export,
        "load" => Token::Load,
        "principal" => Token::Principal,
        "call" => Token::Call,
        "encode" => Token::Encode,
        "decode" => Token::Decode,
        "as" => Token::As,
        "config" => Token::Config,
        "assert" => Token::Assert,
        "let" => Token::Let,
        "fail" => Token::Fail,
        "file" => Token::File,
        "identity" => Token::Identity,
        "fetch" => Token::Fetch,
        "function" => Token::Function,
        "sign" => Token::Sign(<char>),
        "=" => Token::Equals,
        "==" => Token::TestEqual,
        "~=" => Token::SubEqual,
        "!=" => Token::NotEqual,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LSquare,
        "]" => Token::RSquare,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "," => Token::Comma,
        "." => Token::Dot,
        ";" => Token::Semi,
        ":" => Token::Colon,
        "?" => Token::Question,
        "->" => Token::Arrow,
    }
}

pub Commands: Commands = SepBy<Command, ";"> => Commands(<>);

// Command
pub Command: Command = {
    "config" <Text> => Command::Config(<>),
    Exp => Command::Show(<>),
    "assert" <left:Exp> <op:BinOp> <right:Exp> => Command::Assert(op, left, right),
    "let" <id:"id"> "=" <val:Exp> => Command::Let(id, val),
    "export" <Text> => Command::Export(<>),
    "load" <Text> => Command::Load(<>),
    "import" <id:"id"> "=" <uri:Sp<Text>> <did:("as" <Text>)?> =>? {
         let principal = Principal::from_text(&uri.0).map_err(|e| error2(e, uri.1))?;
         Ok(Command::Import(id, principal, did))
    },
    "fetch" <id:Name> <path:Text> => Command::Fetch(id, path),
    "identity" <id:"id"> <path:Text?> => Command::Identity(id, path),
    "function" <name:"id"> "(" <args:SepBy<"id", ",">> ")" "{" <body:SepBy<Command, ";">> "}" => Command::Func {name,args,body},
}

pub Exp: Exp = {
  Arg => <>,
  Variable => <>,
  "file" <Text> => Exp::Blob(<>),
  "fail" <Exp> => Exp::Fail(Box::new(<>)),
  "call" <method:Method> <args:Exps> => Exp::Call{method:Some(method), args, mode: CallMode::Call},
  "call" "as" <proxy:Name> <method:Method> <args:Exps> => Exp::Call{method:Some(method), args, mode: CallMode::Proxy(proxy)},  
  "encode" <method:Method?> <args:Exps> => Exp::Call{method, args, mode: CallMode::Encode},
  "decode" <method:("as" <Method>)?> <blob:Exp> => Exp::Decode{method, blob:Box::new(blob)},
  <func:"id"> "(" <args:SepBy<Exp, ",">> ")" => Exp::Apply(func, args),
}
Variable: Exp = <v:"id"> <path:(<Selector>)*> => Exp::Path(v, path);
Selector: Selector = {
     "?" => Selector::Field("?".to_string()),
     "." <Name> => Selector::Field(<>),
     "[" <Sp<"decimal">> "]" =>? {
       let idx = <>.0.parse::<u64>().map_err(|_| error2("index out of u64 range", <>.1))?;
       Ok(Selector::Index(idx))
     }
}
Method: Method = <canister:Name> "." <method:Name> => Method { canister, method };

BinOp: BinOp = {
  "==" => BinOp::Equal,
  "~=" => BinOp::SubEqual,
  "!=" => BinOp::NotEqual,
}

// Candid Value
Exps: Vec<Exp> = "(" <SepBy<Exp, ",">> ")" => <>;

Arg: Exp = {
    "bool" => Exp::Bool(<>),
    NumLiteral => <>,
    Text => Exp::Text(<>),
    Bytes => {
        let values: Vec<_> = <>.into_iter().map(Exp::Nat8).collect();
        Exp::Vec(values)
    },
    "null" => Exp::Null,
    "opt" <Exp> => Exp::Opt(Box::new(<>)),
    "vec" "{" <SepBy<AnnVal, ";">> "}" => Exp::Vec(<>),
    "record" "{" <Sp<SepBy<RecordField, ";">>> "}" =>? {
        let mut id: u32 = 0;
        let span = <>.1.clone();
        let mut fs: Vec<Field> = <>.0.into_iter().map(|f| {
          match f.id {
            Label::Unnamed(_) => {
              id = id + 1;
              Field { id: Label::Unnamed(id - 1), val: f.val }
            }
            _ => {
              id = f.id.get_id() + 1;
              f
            }
          }
        }).collect();
        fs.sort_unstable_by_key(|Field { id, .. }| id.get_id());
        check_unique(fs.iter().map(|f| &f.id)).map_err(|e| error2(e, span))?;
        Ok(Exp::Record(fs))
    },
    "variant" "{" <VariantField> "}" => Exp::Variant(Box::new(<>), 0),
    "principal" <Sp<Text>> =>? Ok(Exp::Principal(Principal::from_text(&<>.0).map_err(|e| error2(e, <>.1))?)),
    "service" <Sp<Text>> =>? Ok(Exp::Service(Principal::from_text(&<>.0).map_err(|e| error2(e, <>.1))?)),
    "func" <id:Sp<Text>> "." <meth:Name> =>? {
      let id = Principal::from_text(&id.0).map_err(|e| error2(e, id.1))?;
      Ok(Exp::Func(id, meth))
    },
    "(" <AnnVal> ")" => <>,
}

Text: String = {
    Sp<"text"> =>? {
        if std::str::from_utf8(<>.0.as_bytes()).is_err() {
          Err(error2("Not valid unicode text", <>.1))
        } else {
          Ok(<>.0)
        }
    }
}

Bytes: Vec<u8> = {
    "blob" <"text"> => <>.into_bytes(),
}

Number: String = {
    "decimal" => <>,
    // "hex" => num_bigint::BigInt::parse_bytes(<>.as_bytes(), 16).unwrap().to_str_radix(10),
}

AnnVal: Exp = {
    <Exp> => <>,
    <arg:Exp> ":" <typ:Sp<Typ>> =>? {
        let env = TypeEnv::new();
        let typ = env.ast_to_type(&typ.0).map_err(|e| error2(e, typ.1))?;
        Ok(Exp::AnnVal(Box::new(arg), typ))
    }
}

NumLiteral: Exp = {
    <sign:"sign"?> <n:Number> => {
        let num = match sign {
            Some('-') => format!("-{}", n),
            _ => n,
        };
        Exp::Number(num)
    },
    <sign:"sign"?> <n:Sp<"float">> =>? {
        let span = n.1.clone();
        let num = match sign {
            Some('-') => format!("-{}", n.0),
            _ => n.0,
        };
        let f = num.parse::<f64>().map_err(|_| error2("not a float", span))?;
        Ok(Exp::Float64(f))
    },
}

FieldId: u32 = {
    Sp<"decimal"> =>? <>.0.parse::<u32>().map_err(|_| error2("field id out of u32 range", <>.1)),
    Sp<"hex"> =>? u32::from_str_radix(&<>.0, 16).map_err(|_| error2("field id out of u32 range", <>.1)),
}

Field: Field = {
    <n:FieldId> "=" <v:AnnVal> =>? Ok(Field { id: Label::Id(n), val: v }),
    <n:Name> "=" <v:AnnVal> => Field { id: Label::Named(n), val: v },
}

VariantField: Field = {
    Field => <>,
    Name => Field { id: Label::Named(<>), val: Exp::Null },
    FieldId =>? Ok(Field { id: Label::Id(<>), val: Exp::Null }),
}

RecordField: Field = {
    Field => <>,
    AnnVal => Field { id: Label::Unnamed(0), val:<> },
}

// Common util
Name: String = {
    "id" => <>,
    Text => <>,
}

// Type
Typ: IDLType = {
    PrimTyp => <>,
    "opt" <Typ> => IDLType::OptT(Box::new(<>)),
    "vec" <Typ> => IDLType::VecT(Box::new(<>)),
    "blob" => IDLType::VecT(Box::new(IDLType::PrimT(PrimType::Nat8))),
    "record" "{" <Sp<SepBy<RecordFieldTyp, ";">>> "}" =>? {
        let mut id: u32 = 0;
        let span = <>.1.clone();
        let mut fs: Vec<TypeField> = <>.0.iter().map(|f| {
          let label = match f.label {
              Label::Unnamed(_) => { id = id + 1; Label::Unnamed(id - 1) },
              ref l => { id = l.get_id() + 1; l.clone() },
          };
          TypeField { label, typ: f.typ.clone() }
        }).collect();
        fs.sort_unstable_by_key(|TypeField { label, .. }| label.get_id());
        check_unique(fs.iter().map(|f| &f.label)).map_err(|e| error2(e, span))?;
        Ok(IDLType::RecordT(fs))
    },
    "variant" "{" <mut fs:Sp<SepBy<VariantFieldTyp, ";">>> "}" =>? {
        let span = fs.1.clone();
        fs.0.sort_unstable_by_key(|TypeField { label, .. }| label.get_id());
        check_unique(fs.0.iter().map(|f| &f.label)).map_err(|e| error2(e, span))?;
        Ok(IDLType::VariantT(fs.0))
    },
    "func" <FuncTyp> => IDLType::FuncT(<>),
    "service" <ActorTyp> => IDLType::ServT(<>),
    "principal" => IDLType::PrincipalT,
}

PrimTyp: IDLType = {
    "null" => IDLType::PrimT(PrimType::Null),
    "id" => {
      match PrimType::str_to_enum(&<>) {
        Some(p) => IDLType::PrimT(p),
        None => IDLType::VarT(<>),
      }
    },
}

FieldTyp: TypeField = {
    <n:FieldId> ":" <t:Typ> =>? Ok(TypeField { label: Label::Id(n), typ: t }),
    <n:Name> ":" <t:Typ> => TypeField { label: Label::Named(n), typ: t },
}

RecordFieldTyp: TypeField = {
    FieldTyp => <>,
    Typ => TypeField { label: Label::Unnamed(0), typ: <> },
}

VariantFieldTyp: TypeField = {
    FieldTyp => <>,
    Name => TypeField { label: Label::Named(<>), typ: IDLType::PrimT(PrimType::Null) },
    FieldId =>? Ok(TypeField { label: Label::Id(<>), typ: IDLType::PrimT(PrimType::Null) }),
}

TupTyp: Vec<IDLType> = "(" <SepBy<ArgTyp, ",">> ")" => <>;

FuncTyp: FuncType = {
    <args:TupTyp> "->" <rets:TupTyp> <modes:FuncMode*> =>
        FuncType { modes, args, rets },
}

ArgTyp: IDLType = {
    Typ => <>,
    Name ":" <Typ> => <>,
}

ActorTyp: Vec<Binding> = {
    "{" <mut fs:Sp<SepBy<MethTyp, ";">>> "}" =>? {
        let span = fs.1.clone();
        fs.0.sort_unstable_by_key(|Binding { id, .. }| id.clone());
        let labs: Vec<_> = fs.0.iter().map(|f| f.id.clone()).collect();
        check_unique(labs.iter()).map_err(|e| error2(e, span))?;
        Ok(fs.0)
    }
}

MethTyp: Binding = {
    <n:Name> ":" <f:FuncTyp> => Binding { id: n, typ: IDLType::FuncT(f) },
    <n:Name> ":" <id:"id"> => Binding { id: n, typ: IDLType::VarT(id) },
}

FuncMode: FuncMode = {
    "oneway" => FuncMode::Oneway,
    "query" => FuncMode::Query,
}

// Also allows trailing separator
#[inline]
SepBy<T, S>: Vec<T> = {
    <mut v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

#[inline]
Sp<T>: (T, Span) =
    <l: @L> <t: T> <r: @R> => (t, l..r);
